plugins {
    id "multiloader-loader"
    id "net.neoforged.moddev"
    id "com.hypherionmc.modutils.modpublisher" version "2.+"
}

neoForge {
    version = neo_version
    def at = project(":common").file("src/main/resources/META-INF/accesstransformer.cfg")
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parch_mc_version
        mappingsVersion = parch_version
    }
    runs {
        configureEach {
            systemProperty("neoforge.enabledGameTestNamespaces", mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})"
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir "src/main/generated"

publisher {
    apiKeys {
        modrinth System.getenv("MODRINTH_TOKEN")
        curseforge System.getenv("CURSEFORGE_TOKEN")
    }
    setCurseID("$curseforge_id")
    setModrinthID("$modrinth_id")
    setVersionType("$release_type")
    setChangelog("$changelog")

    setDisplayName("$mod_name ${project.version} [NeoForge]")
    setVersion("${mod_version}+neoforge")
    setGameVersions("$mc_version")
    setLoaders("neoforge")

    setArtifact(jar)
    addAdditionalFile(sourcesJar)
    setCurseEnvironment("both")

    curseDepends {}
    modrinthDepends {}
}